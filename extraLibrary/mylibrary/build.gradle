if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion rootProject.ext.android["externalCompileSdkVersion"]
    buildToolsVersion rootProject.ext.android["externalBuildToolsVersion"]

    defaultConfig {
        minSdkVersion rootProject.ext.android["externalMinSdkVersion"]
        targetSdkVersion rootProject.ext.android["externalTargetSdkVersion"]
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }
    buildTypes {
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            //混淆
            minifyEnabled false
            //Zipalign优化
            zipAlignEnabled true
            //前一部分代表系统默认的android程序的混淆文件，该文件已经包含了基本的混淆声明，后一个文件是自己的定义混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            minifyEnabled false
            zipAlignEnabled false
        }
    }

    dataBinding {
        enabled = true;
    }

    sourceSets {
        main {
            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    //合并到主项目时debug文件夹下的java文件就全部被排除了
                    exclude 'debug/**'
                }
            }
        }
    }
//    防止资源冲突
    resourcePrefix "syj_"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support.constraint:constraint-layout:1.1.1'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    api project(':extraLibrary:common')
    implementation "com.android.support:appcompat-v7:${rootProject.ext.android["androidSupportVersion"]}"
    // 路由跳转
    implementation "com.android.support:support-v4:${rootProject.ext.android["androidSupportVersion"]}"
    testImplementation 'junit:junit:4.12'
    //butterKnife
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    //ARouter
    annotationProcessor 'com.alibaba:arouter-compiler:latest.integration'
}
apply from: 'maven-release-aar.gradle'
